buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

version = '0.0.1-SNAPSHOT'
mainClassName = "eu.inginea.vertx.AppLauncher"
ext.mainClass = mainClassName
def vertxCoreVersion = "3.0.0-dev_preview1"
def vertxWebVersion = "3.0.0"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException('''A Haiku:
                                      |  This needs Java 8,
                                      |  You are using something else,
                                      |  Refresh. Try again.'''.stripMargin())
}

repositories {
    //maven { url file('repo').toURL() }
    mavenCentral()
    //    maven {
    //        url = 'http://oss.sonatype.org/content/repositories/snapshots/'
    //    }
    maven {
        url = "http://central.maven.org/maven2/"
    }
}

dependencies {
    compile "io.vertx:vertx-core:${vertxCoreVersion}"
    compile(group:"io.vertx", name:"vertx-web", version:"${vertxWebVersion}") {
        exclude (group:"io.vertx", module:"vertx-core")
    }    
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Class': mainClassName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
