buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

version = '0.0.1-SNAPSHOT'
mainClassName = "eu.inginea.vertx.AppLauncher"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException('''A Haiku:
                                      |  This needs Java 8,
                                      |  You are using something else,
                                      |  Refresh. Try again.'''.stripMargin())
}

repositories {
    maven { url file('repo').toURL() }
    mavenCentral()
    maven {
        url = 'http://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile 'io.vertx:vertx-core:3.0.0-SNAPSHOT'
    
    // should be renamed to vertx-web when available in repos
    // needed to download artefact to local repo and fix pom - missing version for auth dependency
    compile 'io.vertx:vertx-apex:3.0.0-SNAPSHOT' 
        
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Class': mainClassName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}
